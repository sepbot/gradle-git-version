import org.junit.platform.console.options.Details

buildscript {
  ext.versions = [
    kotlin: '1.2.30'
  ]

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: versions.kotlin
    classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: '1.1.0'
  }
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

compileKotlin.kotlinOptions.jvmTarget = '1.8'
compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

repositories {
  mavenCentral()
}

task classpathManifest {
  final outputDir = file("$buildDir/$name")
  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

dependencies {
  compile gradleApi()
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: versions.kotlin

  testCompile gradleTestKit()
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
  testRuntime files(classpathManifest)
}

junitPlatform {
  //noinspection GroovyAssignabilityCheck
  details Details.TREE
}
